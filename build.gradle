plugins {
    id 'java'
    id 'maven-publish'

    id 'io.papermc.paperweight.userdev' version '1.3.7'
    id 'com.github.johnrengelman.shadow' version '7.1.2'
}

java {
    // Configure the java toolchain. This allows gradle to auto-provision JDK 17 on systems that only have JDK 8 installed for example.
    toolchain.languageVersion.set(JavaLanguageVersion.of(17))
}

tasks {
    // Configure reobfJar to run when invoking the build task
    assemble {
        dependsOn(reobfJar)
    }

    compileJava {
        options.encoding = 'UTF-8'// We want UTF-8 for everything

        // Set the release flag. This configures what version bytecode the compiler will emit, as well as what JDK APIs are usable.
        // See https://openjdk.java.net/jeps/247 for more information.
        options.release.set(17)
    }
    javadoc {
        options.encoding = 'UTF-8' // We want UTF-8 for everything
    }
    processResources {
        filteringCharset = 'UTF-8' // We want UTF-8 for everything
    }

    /*
    reobfJar {
      // This is an example of how you might change the output location for reobfJar. It's recommended not to do this
      // for a variety of reasons, however it's asked frequently enough that an example of how to do it is included here.
      outputJar.set(layout.buildDirectory.file("libs/PaperweightTestPlugin-${project.version}.jar"))
    }
     */
}

repositories {
    // Vault and common libraries stuff;
    maven {
        name = 'jitpack'
        url = 'https://jitpack.io'
    }

    mavenLocal()
    mavenCentral()

    // dev-bundle things from paperweight
    maven {
        name = 'papermc-repo'
        url = 'https://repo.papermc.io/repository/maven-public/'
    }
}

dependencies {
    paperweightDevelopmentBundle('io.papermc.paper:dev-bundle:1.19.2-R0.1-SNAPSHOT')

    implementation(
        'org.jetbrains:annotations:16.0.2',
        'com.zaxxer:HikariCP:5.0.1',
        'com.github.SaiintBrisson.command-framework:bukkit:1.3.0'
    )

    compileOnly('com.github.MilkBowl:VaultAPI:1.7.1')
}

shadowJar {
    relocate 'com.zaxxer', 'me.luizotavio.minecraft.hikari'
}
